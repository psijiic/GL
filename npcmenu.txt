!-------------------------------------------------------------------------------
! 2022/10/06 Psijiic
!
! This module allows to config which npcs are visible in stat panel (by default visible are: Mother, Father, Nastja, Tanya, Ashot etc)
! On module call table 'Known people' appears
! Set value Track='YES' in table to add npc to stat panel 
! If all npcs has Track='NO' then default set of people will be shown on stat panel (Mother, Father, Nastja, Tanya, Ashot etc)
! Also filters for Groups, Genders, Relations can be applied to npc list
!-------------------------------------------------------------------------------

! This module should be called from stat_display:
!
! in stat_display lines:
!{
	if npc_QW['A192'] > 0:gs 'show_table','Nastja: <<npc_QW[''A192'']>>','2'
	....
	if mid($start_type,1,2) = 'sg':
		if npc_rel['A29'] > 0:gs'show_table','Mother: <<npc_rel[''A29'']>>','2'
		if npc_rel['A28'] > 0:gs'show_table','Father: <<npc_rel[''A28'']>>','2'
		if npc_rel['A33'] > 0:gs'show_table','Sister: <<npc_rel[''A33'']>>','2'
		if npc_rel['A34'] > 0:gs'show_table','Brother: <<npc_rel[''A34'']>>','2'
	end	
}
!must be replaced to:
!{
	gs 'show_table', '<a href="exec:gs ''npcmenu''">...</a>','2'
	
	if npcme_tracked_npcs_cnt > 0:
		gs 'npcmenu', 'update_table'
		$show_table = $npc_table_stat
	else
		if npc_QW['A192'] > 0:gs 'show_table','Nastja: <<npc_QW[''A192'']>>','2'
		....
		if mid($start_type,1,2) = 'sg':
			if npc_rel['A29'] > 0:gs'show_table','Mother: <<npc_rel[''A29'']>>','2'
			if npc_rel['A28'] > 0:gs'show_table','Father: <<npc_rel[''A28'']>>','2'
			if npc_rel['A33'] > 0:gs'show_table','Sister: <<npc_rel[''A33'']>>','2'
			if npc_rel['A34'] > 0:gs'show_table','Brother: <<npc_rel[''A34'']>>','2'
		end	
	end
}
!-------------------------------------------------------------------------------

if $ARGS[0] = '':
	! Actions on function enter
	
	! store global variables which used in this module as local variables
	npcme_stored_colnum  = colnum
	!npcme_stored_colcnt  = colcnt
	npcme_stored_grpnum  = grpnum
	npcme_stored_npc_num = npc_num
	npcme_stored_npc_cnt = npc_cnt
	
	! clear npc search filter
	$npcme_filter_name = ''
	
	!npc_cnt = arrsize('npc_firstname')
	npc_cnt = aarraynumber
	
	!npcme_initialized = 0
	!last file version number
	!increase it by one if u want to reinitizlize file variables
	npcme_file_version = 7

	! [re]initialization
	!if npcme_initialized = 0:
	if npcme_fver < npcme_file_version:
		npcme_fver = npcme_file_version
		! <definitions below should be in special file?>
		! enum GROUPS
		grpnum = 0
		GRP_OTHERS   = grpnum  &  grpnum += 1
		GRP_COOLKIDS = grpnum  &  grpnum += 1
		GRP_JOCKS    = grpnum  &  grpnum += 1
		GRP_NERDS    = grpnum  &  grpnum += 1
		GRP_GOPNIKS  = grpnum  &  grpnum += 1
		GRP_OUTCASTS = grpnum  &  grpnum += 1
		GRP_TEACHERS = grpnum  &  grpnum += 1
		GRP_FAMILY   = grpnum  &  grpnum += 1
		GRP_FRIENDS  = grpnum  &  grpnum += 1
		groups_count = grpnum
		! groups short name
		$group_name[GRP_OTHERS]   = 'Others'
		$group_name[GRP_COOLKIDS] = 'Cool kids'
		$group_name[GRP_JOCKS]    = 'Jocks'
		$group_name[GRP_NERDS]    = 'Nerds'
		$group_name[GRP_GOPNIKS]  = 'Gopniks'
		$group_name[GRP_OUTCASTS] = 'Outcasts'
		$group_name[GRP_TEACHERS] = 'Teachers'
		$group_name[GRP_FAMILY]   = 'Family'
		$group_name[GRP_FRIENDS]  = 'Friends'
		! </definitions below should be in special file?>
		! define npc of which groups will be shown by default
		npcme_show_group[GRP_OTHERS]   = 1
		npcme_show_group[GRP_COOLKIDS] = 1
		npcme_show_group[GRP_JOCKS]    = 1
		npcme_show_group[GRP_NERDS]    = 1
		npcme_show_group[GRP_GOPNIKS]  = 1
		npcme_show_group[GRP_OUTCASTS] = 1
		npcme_show_group[GRP_TEACHERS] = 1
		npcme_show_group[GRP_FAMILY]   = 1
		npcme_show_group[GRP_FRIENDS]  = 1
		
		$npcme_group_color_default[GRP_OTHERS]   = '#808080'
		$npcme_group_color_default[GRP_COOLKIDS] = '#FF00FF'
		$npcme_group_color_default[GRP_JOCKS]    = '#3BB631'
		$npcme_group_color_default[GRP_NERDS]    = '#00BFFF'
		$npcme_group_color_default[GRP_GOPNIKS]  = 'red'
!'#CC474B'
		$npcme_group_color_default[GRP_OUTCASTS] = '#C19A6B'
		$npcme_group_color_default[GRP_TEACHERS] = 'orange'
		$npcme_group_color_default[GRP_FAMILY]   = '#0072BB'
!'#F19CBB'
!'#006B3C'
		$npcme_group_color_default[GRP_FRIENDS]  = '#007FFF'

		!if npcme_group_color_default_initialized_once = 0:
		!	npcme_group_color_default_initialized_once = 1
		$npcme_group_color[GRP_OTHERS]   = $npcme_group_color_default[GRP_OTHERS]
		$npcme_group_color[GRP_COOLKIDS] = $npcme_group_color_default[GRP_COOLKIDS]
		$npcme_group_color[GRP_JOCKS]    = $npcme_group_color_default[GRP_JOCKS]
		$npcme_group_color[GRP_NERDS]    = $npcme_group_color_default[GRP_NERDS]
		$npcme_group_color[GRP_GOPNIKS]  = $npcme_group_color_default[GRP_GOPNIKS]
		$npcme_group_color[GRP_OUTCASTS] = $npcme_group_color_default[GRP_OUTCASTS]
		$npcme_group_color[GRP_TEACHERS] = $npcme_group_color_default[GRP_TEACHERS]
		$npcme_group_color[GRP_FAMILY]   = $npcme_group_color_default[GRP_FAMILY]
		$npcme_group_color[GRP_FRIENDS]  = $npcme_group_color_default[GRP_FRIENDS]
		!end

		! enum RELATIONS
		REL_COLD       = 0
		REL_ACQUAINTED = 1
		REL_NORMAL     = 2
		REL_GOOD       = 3
		REL_GREAT      = 4
		REL_CNT        = 5
		! relations low value
		REL_COLD_VAL       = 0
		REL_ACQUAINTED_VAL = 20
		REL_NORMAL_VAL     = 40
		REL_GOOD_VAL       = 60
		REL_GREAT_VAL      = 80
		! relations short description
		$rel_name[REL_COLD]       = 'Cold'
		$rel_name[REL_ACQUAINTED] = 'Acquainted'
		$rel_name[REL_NORMAL]     = 'Normal'
		$rel_name[REL_GOOD]       = 'Good'
		$rel_name[REL_GREAT]      = 'Great'
		!
		!$rel_color[REL_COLD]       = 'red'
		!$rel_color[REL_ACQUAINTED] = 'orange'
		!$rel_color[REL_NORMAL]     = 'yellow'
		!$rel_color[REL_GOOD]       = 'green'
		!$rel_color[REL_GREAT]      = 'blue'
		! define which types of relations will be shown by default
		npcme_show_rel[REL_COLD]       = 1
		npcme_show_rel[REL_ACQUAINTED] = 1
		npcme_show_rel[REL_NORMAL]     = 1
		npcme_show_rel[REL_GOOD]       = 1
		npcme_show_rel[REL_GREAT]      = 1


		! enum GENDERS
		GENDER_MALE    = 0
		GENDER_FEMALE  = 1
		GENDER_CNT     = 2
		! relations short description
		$gender_name[GENDER_MALE]   = 'Male'
		$gender_name[GENDER_FEMALE] = 'Female'
		! define which genders will be shown by default
		npcme_show_gender[GENDER_MALE]   = 1
		npcme_show_gender[GENDER_FEMALE] = 1
		
		! clear some variables
		colnum = 0
		:clear_npcme_col_name_plus
		$npcme_col_name_plus[colnum] = ''
		colnum += 1
		if colnum < npcme_column_count: jump 'clear_npcme_col_name_plus'
		
		! COLUMNS: # Photo	Name	Lastname	Nickname	Relation	Age	Birthdate	Groups	Info	Track  BaseID
		colnum = 0
		! #
		npcme_COL_INDEX = colnum
		$npcme_col_name[colnum] = '#'
		npcme_col_show[colnum] = 0
		colnum += 1
		! Photo
		npcme_COL_PHOTO = colnum
		$npcme_col_name[colnum] = 'Photo'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Name
		npcme_COL_NAME = colnum
		$npcme_col_name[colnum] = 'Name'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Lastname
		npcme_COL_LASTNAME = colnum
		$npcme_col_name[colnum] = 'Lastname'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Nickname
		npcme_COL_NICKNAME = colnum
		$npcme_col_name[colnum] = 'Nickname'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Relation
		npcme_COL_RELATION = colnum
		$npcme_col_name[colnum] = 'Relation'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Quest stage
		npcme_COL_QUEST_STAGE = colnum
		$npcme_col_name[colnum] = 'Quest stage'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Age
		npcme_COL_AGE = colnum
		$npcme_col_name[colnum] = 'Age'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Birthdate
		npcme_COL_BIRTHDATE = colnum
		$npcme_col_name[colnum] = 'Birthdate'
		npcme_col_show[colnum] = 0
		colnum += 1
		! Groups
		npcme_COL_GROUPS = colnum
		$npcme_col_name[colnum] = 'Groups'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Sex times
		npcme_COL_SEXTIMES = colnum
		$npcme_col_name[colnum] = 'Sex times'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Dick/Bust size
		npcme_COL_DICK_BUST_SIZE = colnum
		$npcme_col_name[colnum] = 'Dick/ Bust size'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Notes
		npcme_COL_NOTES = colnum
		$npcme_col_name[colnum] = 'Notes'
		npcme_col_show[colnum] = 1
		colnum += 1
		! Track
		npcme_COL_TRACK = colnum
		$npcme_col_name[colnum] = 'Track'
		!$npcme_col_name_plus[colnum] = '<br>all none'
		$npcme_col_name_plus[colnum] = '<br><a href="exec:gs ''npcmenu'',''track_all'' & gt ''npcmenu'',''start''">All</a>|<a href="exec:gs ''npcmenu'',''track_none'' & gt ''npcmenu'',''start''">None</a>'
		npcme_col_show[colnum] = 1
		colnum += 1
		! BaseID
		npcme_COL_BASEID = colnum
		$npcme_col_name[colnum] = 'BaseID'
		npcme_col_show[colnum] = 0
		colnum += 1
		! columns count
		! colcnt = colnum
		npcme_column_count = colnum
		
		! TODO:
		! +columns: Dick/Bust size, Sex times, Interests/Hobbies, Quests, Address (house generator?), Phone, Facebook
		! Change relationship[0..100] to friendship[-100..100] and romance[-100..100]
		
		! tracked   - npc displayed in stats panel
		npcme_show_tracked = 1
		! untracked - npc not displayed in stats panel
		npcme_show_untracked = 1
		
		! show people you don''t meet yet
		npcme_show_unknown_npc = 0
		
		if npc_table_stat_colcnt = 0: npc_table_stat_colcnt = 2
		
		npcme_initialized = 1
	end

	! init preset names
	if npcme_preset_cnt = 0:
		npcme_preset_cnt = 5
		npcme_preset_num = 0
		:next_preset
		$npcme_preset_name[npcme_preset_num] = '<<npcme_preset_num+1>>'
		npcme_preset_num += 1
		if npcme_preset_num < npcme_preset_cnt: jump 'next_preset'
	end
		
	! create array of indexes of npcs sorted by group
	! actually it should check npcstatic1..6 version parameter (? npcstatic_version = 1), but this parameter not exists yet (TODO:add)
	if npcme_npcstatic_cnt < npc_cnt:
		killvar 'npcme_npcs_sorted_by_groups'
		npcme_npcs_sorted_by_groups_cnt = 0
		gs 'npcmenu', 'add_npcs_sorted_by_groups', GRP_COOLKIDS
		gs 'npcmenu', 'add_npcs_sorted_by_groups', GRP_JOCKS
		gs 'npcmenu', 'add_npcs_sorted_by_groups', GRP_NERDS
		gs 'npcmenu', 'add_npcs_sorted_by_groups', GRP_GOPNIKS
		gs 'npcmenu', 'add_npcs_sorted_by_groups', GRP_OUTCASTS
		gs 'npcmenu', 'add_npcs_sorted_by_groups', GRP_TEACHERS
		gs 'npcmenu', 'add_npcs_sorted_by_groups', GRP_FAMILY
		gs 'npcmenu', 'add_npcs_sorted_by_groups', GRP_FRIENDS
		gs 'npcmenu', 'add_npcs_sorted_by_groups', GRP_OTHERS
		
		npcme_npcstatic_cnt = npc_cnt
	end
	
	gs 'npcmenu', 'start'
end
!-------------------------------------------------------------------------------

if $ARGS[0] = 'ex1t':
	
	! restore global variables
	colnum  = npcme_stored_colnum
	!colcnt  = npcme_stored_colcnt
	grpnum  = npcme_stored_grpnum
	npc_num = npcme_stored_npc_num
	npc_cnt = npcme_stored_npc_cnt
	
	npcme_exit = 0
	
	if $menu_loc = 'pav_disco_coolkids':
		! disco bug fix
		gt 'pav_disco', ''
	else
		gt $menu_loc, $menu_arg
	end
end
!-------------------------------------------------------------------------------

if $ARGS[0] = 'start':

	*clr & cla
		
	$npcme_table = ''
	$npcme_text = ''
	
	! short npc table (name+relationship) for show in stats panel (used in module: stat_display)
	!$npc_table_stat=''
	!npc_table_stat_colcnt = 2
	!npc_table_stat_colnum = 1
	
	killvar 'npcme_tracked_npcs'
	npcme_tracked_npcs_cnt = 0

	'<B>Known people:</B>'

	! Show filter of columns which will be shown/hidden
	$npcme_text += '[Columns:'
	colnum = 0
	:next_column
	if npcme_col_show[colnum] = 1:
		$npcme_text += ' <a href="exec:npcme_col_show[<<colnum>>]=0 & gt ''npcmenu'',''start''"><font color="green"><<$npcme_col_name[colnum]>></font></a>'
	else
		$npcme_text += ' <a href="exec:npcme_col_show[<<colnum>>]=1 & gt ''npcmenu'',''start''"><font color="red"><<$npcme_col_name[colnum]>></font></a>'
	end
	colnum += 1
	if colnum < npcme_column_count: jump 'next_column'
	!
	$npcme_text += '] '
	$npcme_text += '<br>'
	

	! Show groups filter
	$npcme_text += '[Groups:'
	grpnum = 0
	:next_group
	if npcme_show_group[grpnum] = 1:
		$npcme_text += ' <a href="exec:gs ''npcmenu'',''show_groups_none'' & npcme_show_group[<<grpnum>>]=1 & gt ''npcmenu'',''start''"><font color="green"><<$group_name[grpnum]>></font></a>'
	else
		$npcme_text += ' <a href="exec:gs ''npcmenu'',''show_groups_none'' & npcme_show_group[<<grpnum>>]=1 & gt ''npcmenu'',''start''"><font color="red"><<$group_name[grpnum]>></font></a>'
	end
	grpnum += 1
	if grpnum < groups_count: jump 'next_group'
	
	$npcme_text += '  <a href="exec:gs ''npcmenu'',''show_groups_all'' & gt ''npcmenu'',''start''">ALL</a> '
	$npcme_text += '<a href="exec:gs ''npcmenu'',''show_groups_none'' & gt ''npcmenu'',''start''">NONE</a>'
	$npcme_text += ']<br>'

	
	! show group colors
	$npcme_text += '[Group colors: '
	if npcme_show_group_colors = 0:
		!$npcme_text += '<a href="exec:npcme_show_group_colors = 1 & gt ''npcmenu'',''start''"><font color="red">Show</font></a> '
		$npcme_text += '<a href="exec:npcme_show_group_colors = 1 & gt ''npcmenu'',''start''">Show</a> '
	else
		!$npcme_text += '<a href="exec:npcme_show_group_colors = 0 & gt ''npcmenu'',''start''"><font color="green">Show</font></a> '
		$npcme_text += '<a href="exec:npcme_show_group_colors = 0 & gt ''npcmenu'',''start''">Hide</a> '

		grpnum = 0
		:next_group_1
		$npcme_color = $npcme_group_color[grpnum]
		if $npcme_color <> '':
			$npcme_text += ' <a href="exec:$npcme_group_color[<<grpnum>>]=input(''Enter group color'') & gt ''npcmenu'',''start''"><font color="<<$npcme_color>>"><<$group_name[grpnum]>>(<<$npcme_color>>)</font></a>'
		else
			$npcme_text += ' <a href="exec:$npcme_group_color[<<grpnum>>]=input(''Enter group color'') & gt ''npcmenu'',''start''"><<$group_name[grpnum]>>(standard)</a>'
		end
		grpnum += 1
		if grpnum < groups_count: jump 'next_group_1'

		$npcme_text += ' <a href="exec:gs ''npcmenu'',''clear_group_colors'' & gt ''npcmenu'',''start''">Clear</a>'
		$npcme_text += ' <a href="exec:gs ''npcmenu'',''default_group_colors'' & gt ''npcmenu'',''start''">Default</a>'
	end
	$npcme_text += ']'

	!$npcme_text += ' [Npcs per row=<a href="exec:npc_table_stat_colcnt=input(''Enter column count'')  & gt ''npcmenu'',''start''"><<npc_table_stat_colcnt>></a>]'
	
	$npcme_text += '<br>'
	
	! show relations filter
	$npcme_text += '[Relation: '
	npcme_relnum = 0
	:next_rel
	if npcme_show_rel[npcme_relnum] = 1:
		$npcme_text += ' <a href="exec:gs ''npcmenu'',''show_rels_none'' & npcme_show_rel[<<npcme_relnum>>]=1 & gt ''npcmenu'',''start''"><font color="green"><<$rel_name[npcme_relnum]>></font></a>'
	else
		$npcme_text += ' <a href="exec:gs ''npcmenu'',''show_rels_none'' & npcme_show_rel[<<npcme_relnum>>]=1 & gt ''npcmenu'',''start''"><font color="red"><<$rel_name[npcme_relnum]>></font></a>'
	end
	npcme_relnum += 1
	if npcme_relnum < REL_CNT: jump 'next_rel'
	!
	$npcme_text += '  <a href="exec:gs ''npcmenu'',''show_rels_all'' & gt ''npcmenu'',''start''">ALL</a> '
	$npcme_text += '<a href="exec:gs ''npcmenu'',''show_rels_none'' & gt ''npcmenu'',''start''">NONE</a>'
	$npcme_text += ']'

	
	! show gender filter
	$npcme_text += '  [Gender:'
	npcme_gennum = 0
	:next_gen
	if npcme_show_gender[npcme_gennum] = 1:
		$npcme_text += ' <a href="exec:npcme_show_gender[<<npcme_gennum>>]=0 & gt ''npcmenu'',''start''"><font color="green"><<$gender_name[npcme_gennum]>></font></a>'
	else
		$npcme_text += ' <a href="exec:npcme_show_gender[<<npcme_gennum>>]=1 & gt ''npcmenu'',''start''"><font color="red"><<$gender_name[npcme_gennum]>></font></a>'
	end
	npcme_gennum += 1
	if npcme_gennum < GENDER_CNT: jump 'next_gen'
	$npcme_text += ']'


	! Show 'Tracked Untracked' filter
	$npcme_text += '  [Show: '
	if npcme_show_tracked = 1:
		$npcme_text += '<a href="exec:npcme_show_tracked=0 & gt ''npcmenu'',''start''"><font color="green">Tracked</font></a>'
	else
		$npcme_text += '<a href="exec:npcme_show_tracked=1 & gt ''npcmenu'',''start''"><font color="red">Tracked</font></a>'
	end
	if npcme_show_untracked = 1:
		$npcme_text += ' <a href="exec:npcme_show_untracked=0 & gt ''npcmenu'',''start''"><font color="green">Untracked</font></a>'
	else
		$npcme_text += ' <a href="exec:npcme_show_untracked=1 & gt ''npcmenu'',''start''"><font color="red">Untracked</font></a>'
	end
	$npcme_text += '] '
	!$npcme_text += '<br>'

	$npcme_text += ' [Npcs per row=<a href="exec:npc_table_stat_colcnt=input(''Enter column count'')  & gt ''npcmenu'',''start''"><<npc_table_stat_colcnt>></a>]'
	
	if npcme_show_unknown_npc = 1:
		$npcme_text += '  [Cheat: <a href="exec:npcme_show_unknown_npc=0 & gt ''npcmenu'',''start''"><font color="green">Show unknown people</font></a>]<br>'
	else
		$npcme_text += '  [Cheat: <a href="exec:npcme_show_unknown_npc=1 & gt ''npcmenu'',''start''"><font color="red">Show unknown people</font></a>]<br>'
	end

	$npcme_text += '[<a href="exec:$npcme_filter_name=input(''Enter name/lastname/nickname to find'') & gt ''npcmenu'',''start''"><font color="green">Find by Name/Lastname/Nickname:</font></a> ''<<$npcme_filter_name>>'''
	$npcme_text += ' <a href="exec:$npcme_filter_name='''' & gt ''npcmenu'',''start''">Clear</a>]<br>'
	
	! presets
	npcme_preset_num = 0
	$npcme_text += '<br><TABLE border=1><TR><TD>Presets:</TD>'
	:next_preset
	$npcme_text += '<TD>"<<$npcme_preset_name[npcme_preset_num]>>": '
	$npcme_text += '<a href="exec:gs ''npcmenu'',''save_preset'',<<npcme_preset_num>> & gt ''npcmenu'',''start''">Save</a>|'
	$npcme_text += '<a href="exec:gs ''npcmenu'',''load_preset'',<<npcme_preset_num>> & npcme_exit = 1 & gt ''npcmenu'',''start''">Load</a>|'
	$npcme_text += '<a href="exec:$npcme_preset_name[<<npcme_preset_num>>]=input(''Enter new preset name:'') & gt ''npcmenu'',''start''">Rename</a></TD>'
	npcme_preset_num += 1
	if npcme_preset_num < npcme_preset_cnt: jump 'next_preset'
	$npcme_text += '</TR></TABLE>'
	!
	!$npcme_text += '<<$npcme_preset2_name>>:'
	!$npcme_text += '<a href="exec:gs ''npcmenu'',''save_preset'',2 &  & gt ''npcmenu'',''start''">Save</a>|'
	!$npcme_text += '<a href="exec:gs ''npcmenu'',''load_preset'',2 & npcme_exit = 1 & gt ''npcmenu'',''start''">Load</a>|'
	!$npcme_text += '<a href="exec:$npcme_preset2_name=input(''Enter preset name:'') & gt ''npcmenu'',''start''">Rename</a> '
	!$npcme_text += ']<br>'

	! Show text before table
	'<<$npcme_text>>'

	! Show columns headers
	$npcme_table += "<TR>"
	colnum = 0
	:next_column_1
	if npcme_col_show[colnum]	= 1: $npcme_table += '<TD><B><<$npcme_col_name[colnum]>><<$npcme_col_name_plus[colnum]>></B></TD>'
	colnum += 1
	if colnum < npcme_column_count: jump 'next_column_1'
	$npcme_table += "</TR>"
	
	killvar 'npcm_in_table'
	
	gs 'npcmenu', 'update_dynpc_table'

	
	npc_total_cnt = npc_cnt + dynpc_cnt
	
	
	!killvar '$npc_tracked'

	
	! <FILL NPCS TABLE>
	$npcme_l_filter_name = LCASE($npcme_filter_name)
	
	npc_num_1 = 1
	npcme_index = 1
	:next_npc
	
	if npc_num_1 <= npc_cnt:
		npc_num = npcme_npcs_sorted_by_groups[npc_num_1]
	
		npcme_npc_group      = npc_grupTipe['A<<npc_num>>']
		npcme_npc_rel        = npc_rel['A<<npc_num>>']
		npcme_npc_QW         = npc_QW['A<<npc_num>>']
		npcme_npc_gender     = npc_gender['A<<npc_num>>']
		npcme_npc_tracked    = npc_tracked['A<<npc_num>>']
		$npcme_npc_firstname = $npc_firstname['A<<npc_num>>']
		$npcme_npc_lastname  = $npc_lastname['A<<npc_num>>']
		$npcme_npc_nickname  = $npc_nickname['A<<npc_num>>']
		npcme_npc_dob        = npc_dob['A<<npc_num>>']
		npcme_npc_sex        = npc_sex['A<<npc_num>>']
		npcme_npc_dick       = npc_dick['A<<npc_num>>']
		npcme_npc_bust       = npc_bust['A<<npc_num>>']
		
		if npcme_npc_rel = 0: npcme_npc_rel = npc_rel_TMP['A<<npc_num>>']
		
		if npcme_npc_tracked = 1:
			npcme_tracked_npcs[npcme_tracked_npcs_cnt] = npc_num
			npcme_tracked_npcs_cnt += 1
		end
	else
		npc_num = npc_num_1
	
		dynpc_num = npc_num - npc_cnt - 1
		npcme_npc_group      = dynpc_grupTipe[dynpc_num]
		npcme_npc_rel        = dynpc_rel[dynpc_num]
		npcme_npc_QW         = dynpc_QW[dynpc_num]
		npcme_npc_gender     = dynpc_gender[dynpc_num]
		npcme_npc_tracked    = dynpc_tracked[dynpc_num]
		$npcme_npc_firstname = $dynpc_firstname[dynpc_num]
		$npcme_npc_lastname  = $dynpc_lastname[dynpc_num]
		$npcme_npc_nickname  = $dynpc_nickname[dynpc_num]
		npcme_npc_dob        = dynpc_dob[dynpc_num]
		npcme_npc_sex        = dynpc_sex[dynpc_num]
		npcme_npc_dick       = dynpc_dick[dynpc_num]
		npcme_npc_bust       = dynpc_bust[dynpc_num]
	end

	
	!group 
	!npcme_npc_group = npc_grupTipe['A<<npc_num>>']

	!npcme_npc_rel = npc_rel['A<<npc_num>>']
	if npcme_npc_rel >= REL_GREAT_VAL:
		npcme_npc_rel_level = REL_GREAT
	elseif npcme_npc_rel >= REL_GOOD_VAL:
		npcme_npc_rel_level = REL_GOOD
	elseif npcme_npc_rel >= REL_NORMAL_VAL:
		npcme_npc_rel_level = REL_NORMAL
	elseif npcme_npc_rel >= REL_ACQUAINTED_VAL:
		npcme_npc_rel_level = REL_ACQUAINTED
	else
		npcme_npc_rel_level = REL_COLD
	end

	!npcme_npc_gender = npc_gender['A<<npc_num>>']

	!npcme_c1 = (npc_rel['A<<npc_num>>'] > 0 and npcme_show_unknown_npc = 0) or npcme_show_unknown_npc = 1
	!npcme_c2 = (npc_QW['A<<npc_num>>'] > 0 and npcme_show_unknown_npc = 0) or npcme_show_unknown_npc = 1
	npcme_c1 = npcme_npc_rel > 0 or npcme_npc_QW > 0 or npcme_npc_sex > 0
	!npcme_c1 = npcme_npc_rel > 0
	!npcme_c2 = npcme_npc_QW > 0

	npcme_c3 = npcme_show_gender[npcme_npc_gender]
	npcme_c4 = npcme_show_rel[npcme_npc_rel_level]
	npcme_c5 = npcme_show_group[npcme_npc_group]
	!npcme_c6 = ($npc_tracked['A<<npc_num>>'] = 1 and npcme_show_tracked = 1) or ($npc_tracked['A<<npc_num>>'] = 0 and npcme_show_untracked = 1)
	npcme_c6 = (npcme_npc_tracked = 1 and npcme_show_tracked = 1) or (npcme_npc_tracked = 0 and npcme_show_untracked = 1)
	

	
	!'<<npcme_c1>> <<npcme_c2>> <<npcme_c3>> <<npcme_c4>> <<npcme_c5>> npc_cnt=<<npc_cnt>>'
	npcm_in_table[npc_num] = 0
	
	!if npc_rel['A<<npc_num>>'] > 0 and npcme_show_gender[npcme_npc_gender] = 1 and npcme_show_rel[npcme_npc_rel_level] = 1 and npcme_show_group[npcme_npc_group] = 1 and (($npc_tracked['A<<npc_num>>'] = 1 and npcme_show_tracked = 1) or ($npc_tracked['A<<npc_num>>'] = 0 and npcme_show_untracked = 1)):
	if (npcme_c1 or npcme_show_unknown_npc=1) and npcme_c3 and npcme_c4 and npcme_c5 and npcme_c6:

		! filter by name/lastname/nickname
		$npcme_l_firstname = LCASE($npcme_npc_firstname)
		$npcme_l_lastname  = LCASE($npcme_npc_lastname)
		$npcme_l_nickname  = LCASE($npcme_npc_nickname)
		if $npcme_filter_name <> '':
			if INSTR(0,$npcme_l_firstname,$npcme_l_filter_name)>0:
				npcme_c7 = 1
			elseif INSTR(0,$npcme_l_lastname,$npcme_l_filter_name)>0:
				npcme_c7 = 1
			elseif INSTR(0,$npcme_l_nickname,$npcme_l_filter_name)>0:
				npcme_c7 = 1
			else
				npcme_c7 = 0
			end
		else
			npcme_c7 = 1
		end
		
		if npcme_c7 = 1:
		
			npcm_in_table[npc_num] = 1

			$npcme_table += "<TR>"

			! #
			if npcme_col_show[npcme_COL_INDEX] = 1: $npcme_table += '<TD><<npcme_index>></TD>'

			! PHOTO
			if npcme_col_show[npcme_COL_PHOTO] = 1:
				if npc_num <= npc_cnt:
					$npcme_table += '<TD><img src="images\characters\shared\headshots_main\<<npc_num>>.jpg"></TD>'
				else
					$path = $dynpc_pic[npc_num-npc_cnt-1]
					$npcme_table += '<TD><img width="100" height="100" src="<<$path>>"></TD>'
					!$npcme_table += '<TD><<$path>></TD>'
					!$npcme_table += '<TD><<npc_num-npc_cnt-1-dynpc_lovers_offset>></TD>'
				end
			end
			
			! FIRST NAME
			!if npcme_col_show[npcme_COL_NAME] = 1: $npcme_table += "<TD><<$npc_firstname['A<<npc_num>>']>></TD>"
			if npcme_col_show[npcme_COL_NAME] = 1: $npcme_table += "<TD><<$npcme_npc_firstname>></TD>"
			
			! LAST NAME
			!if npcme_col_show[npcme_COL_LASTNAME] = 1: $npcme_table += "<TD><<$npc_lastname['A<<npc_num>>']>></TD>"
			if npcme_col_show[npcme_COL_LASTNAME] = 1: $npcme_table += "<TD><<$npcme_npc_lastname>></TD>"
			
			! NICKNAME
			if npcme_col_show[npcme_COL_NICKNAME] = 1:
				!if $npc_nickname['A<<npc_num>>'] <> '':
				if $npcme_npc_nickname <> '':
					!$npcme_table += "<TD>'<<$npc_nickname['A<<npc_num>>']>>'</TD>"
					$npcme_table += "<TD>'<<$npcme_npc_nickname>>'</TD>"
				else
					$npcme_table += "<TD></TD>"
				end
			end
			
			! RELATIONSHIP
			!if npcme_col_show[npcme_COL_RELATION] = 1: $npcme_table += "<TD><<npc_rel['A<<npc_num>>']>></TD>"
			if npcme_col_show[npcme_COL_RELATION] = 1: $npcme_table += "<TD><<npcme_npc_rel>></TD>"
			
			! QUEST STAGE
			if npcme_col_show[npcme_COL_QUEST_STAGE] = 1:
				!if npc_QW['A<<npc_num>>'] > 0:
				if npcme_npc_QW > 0:
					!$npcme_table += "<TD><<npc_QW['A<<npc_num>>']>></TD>"
					$npcme_table += "<TD><<npcme_npc_QW>></TD>"
				else
					$npcme_table += "<TD>none</TD>"
				end
			end
			
			! BIRTHDATE & AGE
			!npcme_currnpc_year  = mid(npc_dob['A<<npc_num>>'], 1, 4)
			!npcme_currnpc_month = mid(npc_dob['A<<npc_num>>'], 5, 2)
			!npcme_currnpc_day   = mid(npc_dob['A<<npc_num>>'], 7, 2)
			npcme_currnpc_year  = mid(npcme_npc_dob, 1, 4)
			npcme_currnpc_month = mid(npcme_npc_dob, 5, 2)
			npcme_currnpc_day   = mid(npcme_npc_dob, 7, 2)
			npcme_currnpc_age   = year - npcme_currnpc_year
			if npcme_currnpc_month < month:
				npcme_currnpc_age -= 1
			elseif npcme_currnpc_month = month and npcme_currnpc_day < day:
				npcme_currnpc_age -= 1
			end
			if npcme_col_show[npcme_COL_AGE] = 1: $npcme_table += "<TD><<npcme_currnpc_age>></TD>"
			if npcme_col_show[npcme_COL_BIRTHDATE] = 1: $npcme_table += "<TD><<npcme_currnpc_year>>/<<npcme_currnpc_month>>/<<npcme_currnpc_day>></TD>"

			! GROUP
			if npcme_col_show[npcme_COL_GROUPS] = 1: $npcme_table += "<TD><<$group_name[npcme_npc_group]>></TD>"

			! SEXTIMES
			if npcme_col_show[npcme_COL_SEXTIMES] = 1:
				!if npc_sex['A<<npc_num>>'] > 0 :
				if npcme_npc_sex > 0 :
					!$npcme_table += "<TD><<npc_sex['A<<npc_num>>']>></TD>"
					$npcme_table += "<TD><<npcme_npc_sex>></TD>"
				else 
					$npcme_table += "<TD>never</TD>"
				end
			end
			
			! DICK_BUST_SIZE
			if npcme_col_show[npcme_COL_DICK_BUST_SIZE] = 1:
				! Sveta knows bust size of any girl and
				! knows dick size of man she had sex with
				$npcme_table += "<TD>"
				!if npc_sex['A<<npc_num>>'] > 0:
				if npcme_npc_sex > 0:
					!if npc_dick['A<<npc_num>>'] > 0: $npcme_table += '<center><<npc_dick[''A<<npc_num>>'']>> cm</center>'
					if npcme_npc_dick > 0: $npcme_table += '<center><<npcme_npc_dick>> cm</center>'
				else
					!if npc_dick['A<<npc_num>>'] > 0: $npcme_table += '<center>?</center>'
					if npcme_npc_dick > 0: $npcme_table += '<center>?</center>'
				end
				
				!if npc_bust['A<<npc_num>>'] > 0:
				if npcme_npc_bust > 0:
					!gs 'npcmenu', 'tits_info', npc_bust['A<<npc_num>>']
					gs 'npcmenu', 'tits_info', npcme_npc_bust
					$npcme_table += '<a href="exec:view ''images/pc/body/tits/t<<npcme_tits>>.jpg''"><center><<$npcme_titsize>></center></a>'
				end
				$npcme_table += "</TD>"
			end
			
			! INFO
			!if npcme_col_show[npcme_COL_NOTES] = 1: $npcme_table += "<TD><<$npc_notes['A<<npc_num>>']>></TD>"
			if npcme_col_show[npcme_COL_NOTES] = 1:
				if npc_num <= npc_cnt:
					$npcme_table += "<TD><<$npc_notes['A<<npc_num>>']>></TD>"
				else
					$npcme_table += "<TD><<$dynpc_notes[dynpc_num]>></TD>"
				end
			end

			! TRACK (show npc in stats panel)
			if npcme_col_show[npcme_COL_TRACK] = 1: 
				!if $npc_tracked['A<<npc_num>>'] = 1:
				
				if npcme_npc_tracked = 1:
					!$npcme_table += '<TD><a href="exec:$npc_tracked[''A<<npc_num>>'']=0 & gt ''npcmenu'',''start''"><font color="green">YES</color></a></TD>'
					if npc_num <= npc_cnt:
						$npcme_table += '<TD><a href="exec:npc_tracked[''A<<npc_num>>'']=0 & gt ''npcmenu'',''start''"><font color="green">YES</color></a></TD>'
					else
						!$npcme_table += '<TD><a href="exec:dynpc_tracked[dynpc_num]=0 & gt ''npcmenu'',''start''"><font color="green">YES</color></a></TD>'
						$npcme_table += '<TD><a href="exec:gs ''npcmenu'',''dynpc_untrack'',<<dynpc_num>> & gt ''npcmenu'',''start''"><font color="green">YES</color></a></TD>'
					end
				else
					if npc_num <= npc_cnt:
						$npcme_table += '<TD><a href="exec:npc_tracked[''A<<npc_num>>'']=1 & gt ''npcmenu'',''start''"><font color="red">NO</color></a></TD>'
					else
						!$npcme_table += '<TD><a href="exec:dynpc_tracked[dynpc_num]=1 & gt ''npcmenu'',''start''"><font color="red">NO</color></a></TD>'
						$npcme_table += '<TD><a href="exec:gs ''npcmenu'',''dynpc_track'',<<dynpc_num>> & gt ''npcmenu'',''start''"><font color="red">NO</color></a></TD>'
					end
				end
			end

			! BaseID
			if npcme_col_show[npcme_COL_BASEID] = 1: $npcme_table += "<TD><<npc_num>></TD>"
			
			$npcme_table += "</TR>"
			
			npcme_index += 1
		end
	end
	
	!{
	!if $npc_tracked['A<<npc_num>>'] = 1:
	if npcme_npc_tracked = 1:
		npcme_tracked_npcs[npcme_tracked_npcs_cnt] = npc_num
		npcme_tracked_npcs_cnt += 1
	end
	}
			
	npc_num_1 += 1
	!if npc_num <= npc_cnt: jump 'next_npc'
	if npc_num_1 <= npc_total_cnt: jump 'next_npc'
	
	
	!if $npc_table_stat <> '': $npc_table_stat = '<TR><TD><a href="exec:gt ''npcmenu''">...</a></TD>' + $npc_table_stat
	! </FILL NPCS TABLE>

	! Show npcs table
	'<BR><TABLE BORDER=1><<$npcme_table>></TABLE>'
	
	gs 'npcmenu', 'update_table'

	if npcme_exit = 1:
		gt 'npcmenu', 'ex1t'
	else
		act 'Exit the menu': gt 'npcmenu', 'ex1t'
	end
	
	!{
	if $menu_loc = 'pav_disco_coolkids':
		! disco bug fix
		act 'Exit the menu': gs 'npcmenu', 'ex1t' & gt 'pav_disco', ''
	else
		!act 'Exit the menu': input("<<$menu_loc>>, <<$menu_arg>>") & gs 'npcmenu', 'ex1t' & gt $menu_loc, $menu_arg
		act 'Exit the menu': gs 'npcmenu', 'ex1t' & gt $menu_loc, $menu_arg
	end
	}
end
!-------------------------------------------------------------------------------

!npcm_in_table -> npcme_npc_in_table
!+ npcme_dynpc_in_table

! [1]
if $ARGS[0] = 'track_all':
	npcme1_npc_num = 1
	:next_npc
	!if npc_rel['A<<npcme1_npc_num>>'] > 0: 
	if npcm_in_table[npcme1_npc_num] = 1: npc_tracked['A<<npcme1_npc_num>>'] = 1
	npcme1_npc_num += 1
	if npcme1_npc_num <= npc_cnt: jump 'next_npc'
end
!-------------------------------------------------------------------------------

! [2]
if $ARGS[0] = 'track_none':
	npcme2_npc_num = 1
	:next_npc
	if npcm_in_table[npcme2_npc_num] = 1: npc_tracked['A<<npcme2_npc_num>>'] = 0
	npcme2_npc_num += 1
	if npcme2_npc_num <= npc_cnt: jump 'next_npc'
end
!-------------------------------------------------------------------------------

! [3]
if $ARGS[0] = 'show_groups_all':
	npcme3_grpnum = 0
	:next_group
	npcme_show_group[npcme3_grpnum] = 1
	npcme3_grpnum += 1
	if npcme3_grpnum < groups_count: jump 'next_group'
end
!-------------------------------------------------------------------------------

! [4]
if $ARGS[0] = 'show_groups_none':
	npcme4_grpnum = 0
	:next_group
	npcme_show_group[npcme4_grpnum] = 0
	npcme4_grpnum += 1
	if npcme4_grpnum < groups_count: jump 'next_group'
end
!-------------------------------------------------------------------------------

! [5]
if $ARGS[0] = 'show_rels_all':
	npcme5_relnum = 0
	:next_rel
	npcme_show_rel[npcme5_relnum] = 1
	npcme5_relnum += 1
	if npcme5_relnum < REL_CNT: jump 'next_rel'
end
!-------------------------------------------------------------------------------

! [6]
if $ARGS[0] = 'show_rels_none':
	npcme6_relnum = 0
	:next_rel
	npcme_show_rel[npcme6_relnum] = 0
	npcme6_relnum += 1
	if npcme6_relnum < REL_CNT: jump 'next_rel'
end
!-------------------------------------------------------------------------------

! [7]
if $ARGS[0] = 'update_table':
	
	gs 'npcmenu', 'update_dynpc_table'
	
	npc_total_cnt = npc_cnt + dynpc_cnt
	
	!$npc_table_stat = ''
	if npc_table_stat_colcnt > 0: $npc_table_stat = '<TR><TD><a href="exec:gt ''npcmenu''">...</a></TD>'
	
	npcme7_colnum = 1
	
	!input('<<npc_total_cnt>> <<npcme_tracked_npcs_cnt>>')
	
	if npcme_tracked_npcs_cnt = 0: jump 'end'
	npcme7_i = 0
	:next_npc
	if npcme7_colnum = 0: $npc_table_stat += "<TR>"
	npcme7_npc_num = npcme_tracked_npcs[npcme7_i]

	npcme7_npc_group = npc_grupTipe['A<<npcme7_npc_num>>']
	$npcme7_npc_usedname = $npc_usedname['A<<npcme7_npc_num>>']
	npcme7_npc_rel = npc_rel['A<<npcme7_npc_num>>']
	if npcme7_npc_rel = 0: npcme7_npc_rel = npc_QW['A<<npcme7_npc_num>>']
	if npcme7_npc_rel = 0: npcme7_npc_rel = npc_rel_TMP['A<<npcme7_npc_num>>']
	
	if npcme_show_group_colors = 1 and $npcme_group_color[npcme7_npc_group] <> '':
		$npc_table_stat += "<TD><<npcme7_npc_rel>> <font color='<<$npcme_group_color[npcme7_npc_group]>>'><<$npcme7_npc_usedname>></font></TD>"
		!$npc_table_stat += "<TD><font color='<<$npcme_group_color[npcme7_npc_group]>>'><<$npcme7_npc_usedname>></font> <<npcme7_npc_rel>></TD>"
		
		!$npc_table_stat += "<TD><font color='<<$npcme_group_color[npcme7_npc_group]>>'><<$npcme7_npc_usedname>></font></TD>"
		!$npc_table_stat += "<TD><<npcme7_npc_rel>></TD>"
	else
		$npc_table_stat += "<TD><<npcme7_npc_rel>> <<$npcme7_npc_usedname>></TD>"
		!$npc_table_stat += "<TD><<$npcme7_npc_usedname>>: <<npcme7_npc_rel>></TD>"
		
		!$npc_table_stat += "<TD><<$npcme7_npc_usedname>></TD>"
		!$npc_table_stat += "<TD><<npcme7_npc_rel>></TD>"
	end

	npcme7_colnum += 1
	if npcme7_colnum >= npc_table_stat_colcnt:
		$npc_table_stat += "</TR>"
		npcme7_colnum = 0
	end
	npcme7_i += 1
	if npcme7_i < npcme_tracked_npcs_cnt: jump 'next_npc'
	:end

	
	if npcme_tracked_dynpcs_cnt = 0: jump 'end_1'
	npcme7_i = 0
	:next_npc_1
	if npcme7_colnum = 0: $npc_table_stat += "<TR>"
	
	npcme7_dynpc_num = npcme_tracked_dynpcs[npcme7_i]

	!npcme7_dynpc_num = npcme7_npc_num - npc_cnt - 1
	npcme7_npc_group = dynpc_grupTipe[npcme7_dynpc_num]
	$npcme7_npc_usedname = $dynpc_usedname[npcme7_dynpc_num]
	npcme7_npc_rel = dynpc_rel[npcme7_dynpc_num]
	if npcme7_npc_rel = 0: npcme7_npc_rel = dynpc_QW[npcme7_dynpc_num]
	
	if npcme_show_group_colors = 1 and $npcme_group_color[npcme7_npc_group] <> '':
		$npc_table_stat += "<TD><<npcme7_npc_rel>> <font color='<<$npcme_group_color[npcme7_npc_group]>>'><<$npcme7_npc_usedname>></font></TD>"
		!$npc_table_stat += "<TD><font color='<<$npcme_group_color[npcme7_npc_group]>>'><<$npcme7_npc_usedname>></font> <<npcme7_npc_rel>></TD>"
		
		!$npc_table_stat += "<TD><font color='<<$npcme_group_color[npcme7_npc_group]>>'><<$npcme7_npc_usedname>></font></TD>"
		!$npc_table_stat += "<TD><<npcme7_npc_rel>></TD>"
	else
		$npc_table_stat += "<TD><<npcme7_npc_rel>> <<$npcme7_npc_usedname>></TD>"
		!$npc_table_stat += "<TD><<$npcme7_npc_usedname>>: <<npcme7_npc_rel>></TD>"
		
		!$npc_table_stat += "<TD><<$npcme7_npc_usedname>></TD>"
		!$npc_table_stat += "<TD><<npcme7_npc_rel>></TD>"
	end

	npcme7_colnum += 1
	if npcme7_colnum >= npc_table_stat_colcnt:
		$npc_table_stat += "</TR>"
		npcme7_colnum = 0
	end
	npcme7_i += 1
	if npcme7_i < npcme_tracked_dynpcs_cnt: jump 'next_npc_1'
	:end_1
	
	!if npc_table_stat_colcnt > 0: $npc_table_stat += '<TR><TD><a href="exec:gt ''npcmenu''">...</a></TD>'
	
end

! [8]
if $ARGS[0] = 'tits_info':
	if ARGS[1] <= 5:
		npcme_tits = 0
		$npcme_titsize = 'AA cup'
	elseif ARGS[1] <= 10:
		npcme_tits = 1
		$npcme_titsize = 'A cup'
	elseif ARGS[1] <= 15:
		npcme_tits = 2
		$npcme_titsize = 'B cup'
	elseif ARGS[1] <= 20:
		npcme_tits = 3
		$npcme_titsize = 'C cup'
	elseif ARGS[1] <= 25:
		npcme_tits = 4
		$npcme_titsize = 'D cup'
	elseif ARGS[1] <= 30:
		npcme_tits = 5
		$npcme_titsize = 'E cup'
	elseif ARGS[1] <= 35:
		npcme_tits = 6
		$npcme_titsize = 'F cup'
	elseif ARGS[1] <= 40:
		npcme_tits = 7
		$npcme_titsize = 'G cup'
	elseif ARGS[1] <= 45:
		npcme_tits = 8
		$npcme_titsize = 'H cup'
	elseif ARGS[1] <= 50:
		npcme_tits = 9
		$npcme_titsize = 'I cup'
	elseif ARGS[1] <= 55:
		npcme_tits = 10
		$npcme_titsize = 'J cup'
	else
		npcme_tits = 11
		$npcme_titsize = 'K cup'
	end
end

! [9]
if $ARGS[0] = 'clear_group_colors':
	npcme9_grpnum = 0
	:next_group
	$npcme_group_color[npcme9_grpnum] = ''
	npcme9_grpnum += 1
	if npcme9_grpnum < groups_count: jump 'next_group'
end

! [10]
if $ARGS[0] = 'default_group_colors':
	npcme10_grpnum = 0
	:next_group
	$npcme_group_color[npcme10_grpnum] = $npcme_group_color_default[npcme10_grpnum]
	npcme10_grpnum += 1
	if npcme10_grpnum < groups_count: jump 'next_group'
end

! [11]
if $ARGS[0] = 'dynpc_track':
	npcme11_dynpc_num = ARGS[1]
	!input('<<npcme11_dynpc_num>>')
	if npcme11_dynpc_num < dynpc_lovers_offset:
		dynpc_tracked[npcme11_dynpc_num] = 1
	else
		!input("dynpc_lovers_offset=<<dynpc_lovers_offset>> dynpc_cnt=<<dynpc_cnt>>")
		npcme11_dynpc_num = dynpc_lovers_offset
		npcme11_lover_num = 0
		:next_npc
		dynpc_tracked[npcme11_dynpc_num] = 1
		npcme11_dynpc_num += 1
		npcme11_lover_num += 1
		! track up to 3 lovers
		if npcme11_lover_num < 3: jump 'next_npc'
		!if npcme11_lover_num < npcme_lover_count: jump 'next_npc'
		!if npcme11_dynpc_num < dynpc_cnt: jump 'next_npc'
	end
end

! [12]
if $ARGS[0] = 'dynpc_untrack':
	
	npcme12_dynpc_num = ARGS[1]
	if npcme12_dynpc_num < dynpc_lovers_offset:
		dynpc_tracked[npcme12_dynpc_num] = 0
	else
		npcme12_dynpc_num = dynpc_lovers_offset
		npcme12_lover_num = 0
		:next_npc
		dynpc_tracked[npcme12_dynpc_num] = 0
		npcme12_dynpc_num += 1
		npcme12_lover_num += 1
		! untrack up to 3 lovers
		if npcme12_lover_num < 3: jump 'next_npc'
		!if npcme12_lover_num < npcme_lover_count: jump 'next_npc'
		!if npcme12_dynpc_num < dynpc_cnt: jump 'next_npc'
		
		
	end
end

! [13]
if $ARGS[0] = 'update_dynpc_table':
	killvar 'dynpc_grupTipe'
	killvar 'dynpc_rel'
	killvar 'dynpc_QW'
	killvar 'dynpc_gender'
	!killvar 'dynpc_tracked'
	killvar '$dynpc_firstname'
	killvar '$dynpc_lastname'
	killvar '$dynpc_nickname'
	killvar 'dynpc_dob'
	killvar 'dynpc_sex'
	killvar 'dynpc_dick'
	killvar 'dynpc_bust'
	killvar '$dynpc_pic'
	killvar '$dynpc_notes'
	
	! not dynpc table info few strings below, but it must be updated too:
	! set therapist relation to non zero if sveta had therapt sessions but relation with him is still equal to zero
	if npc_rel['A186']=0 and npc_QW['A186']=0: npc_rel_TMP['A186'] = hypnoStage
	! set kolka sex times to non zero if sveta had sex with him
	if npc_sex['A34'] = 0 and brotherSexCount > 0: npc_sex['A34'] = brotherSexCount
	! hunter Andrei
	if npc_rel['A172']=0: npc_rel_TMP['A172'] = huntersAndreiQw
	! hunter Igor
	if npc_rel['A173']=0: npc_rel_TMP['A173'] = huntersIgorQw
	! hunter Sergei
	if npc_rel['A174']=0: npc_rel_TMP['A174'] = huntersSergeiQw
	
	
	
	! Create dynamic array (dynpc) which contains npcs are not present in npcstatic1..npcstatic6 files
	! They are: Alla, Masha, 3 hunters and all boy/girlfriends (more npcs can be added if I missed somebody )
	dynpc_cnt = 0
	$dynpc_firstname[dynpc_cnt] = 'Alla'
	$dynpc_usedname[dynpc_cnt]  = 'Alla'
	dynpc_rel[dynpc_cnt]        = alla
	dynpc_gender[dynpc_cnt]     = GENDER_FEMALE
	dynpc_cnt += 1
	!
	$dynpc_firstname[dynpc_cnt] = 'Masha'
	$dynpc_usedname[dynpc_cnt]  = 'Masha'
	dynpc_rel[dynpc_cnt]        = masha
	dynpc_gender[dynpc_cnt]     = GENDER_FEMALE
	dynpc_cnt += 1
	!{
	$dynpc_firstname[dynpc_cnt] = 'Andrew'
	$dynpc_usedname[dynpc_cnt]  = 'Hunter Andrew'
	dynpc_QW[dynpc_cnt]         = huntersAndreiQw
	dynpc_gender[dynpc_cnt]     = GENDER_MALE
	$dynpc_notes[dynpc_cnt]     = 'Hunter'
	dynpc_cnt += 1
	!
	$dynpc_firstname[dynpc_cnt] = 'Sergei'
	$dynpc_usedname[dynpc_cnt]  = 'Hunter Sergei'
	dynpc_QW[dynpc_cnt]         = huntersSergeiQw
	dynpc_gender[dynpc_cnt]     = GENDER_MALE
	$dynpc_notes[dynpc_cnt]     = 'Hunter'
	dynpc_cnt += 1
	!
	$dynpc_firstname[dynpc_cnt] = 'Igor'
	$dynpc_usedname[dynpc_cnt]  = 'Hunter Igor'
	dynpc_QW[dynpc_cnt]         = huntersIgorQw
	dynpc_gender[dynpc_cnt]     = GENDER_MALE
	$dynpc_notes[dynpc_cnt]     = 'Hunter'
	dynpc_cnt += 1
	}
	!{
	grpnum = 1
	:next_group
	$dynpc_firstname[dynpc_cnt] = '<<$group_name[grpnum]>>'
	!$dynpc_usedname[dynpc_cnt]  = '<font color=''<<$npcme_group_color>>''><<$group_name[grpnum]>></font>'
	$dynpc_usedname[dynpc_cnt]  = '<<$group_name[grpnum]>>'
	dynpc_rel[dynpc_cnt]        = iif(grupvalue[grpnum]>0,grupvalue[grpnum],1)
	dynpc_gender[dynpc_cnt]     = GENDER_MALE
	$dynpc_notes[dynpc_cnt]     = 'Reputation with group'
	dynpc_cnt += 1	
	grpnum += 1
	if grpnum < groups_count: jump 'next_group'	
	}
	!
	dynpc_lovers_offset = dynpc_cnt
	npcme_lover_num = 0
	!lover_count = arrsize($loverdesc)
	!npcme_lover_count = arrsize('pcs_lovers')
	
	if pcs_lovers[2] = 1:
		npcme_lover_count = 3
	elseif pcs_lovers[1] = 1:
		npcme_lover_count = 2
	elseif pcs_lovers[0] = 1:
		npcme_lover_count = 1
	else
		npcme_lover_count = 0
	end

	!if pcs_lovers[0] = 1: npcme_lover_count += 1
	!if pcs_lovers[1] = 1: npcme_lover_count += 1
	!if pcs_lovers[2] = 1: npcme_lover_count += 1
	
	if npcme_lover_count = 0: jump 'end'
	:next_lover
	$dynpc_firstname[dynpc_cnt] = '<<$loverdesc[npcme_lover_num]>>'
	$dynpc_usedname[dynpc_cnt]  = 'BF <<$loverdesc[npcme_lover_num]>>'
	dynpc_rel[dynpc_cnt]        = loverrelation[npcme_lover_num]
	dynpc_gender[dynpc_cnt]     = loverGender[npcme_lover_num]
	if dynpc_gender[dynpc_cnt] = GENDER_MALE:
		dynpc_dick[dynpc_cnt] = loverdick[npcme_lover_num]
	else
		dynpc_bust[dynpc_cnt] = titBoy[npcme_lover_num]
	end
	$dynpc_pic[dynpc_cnt]       = 'images/characters/shared/headshots_generic/'+lover_picture[npcme_lover_num]+'.jpg'
	dynpc_sex[dynpc_cnt]        = boyonce[npcme_lover_num]
	if dynpc_gender[dynpc_cnt] = GENDER_MALE:
	
		$dynpc_notes[dynpc_cnt]     = 'Boyfriend<br><a href="exec:npcme_curloverrel = loverrelation[<<npcme_lover_num>>] & lover_number = <<npcme_lover_num>> & *clr & cla & gs ''lover_likes'', ''allPref'' & loverrelation[<<npcme_lover_num>>] = npcme_curloverrel & act ''Hang up.'': gt ''npcmenu'', ''start''">Call him and ask how he thinks you look?</a>'
		!$dynpc_notes[dynpc_cnt]     = 'Boyfriend<br><a href="exec:lover_number = <<npcme_lover_num>> & *clr & cla & gs ''lover_likes'', ''allPref'' & act ''Hang up.'': gt ''npcmenu'', ''start''">Call him and ask how he thinks you look?</a>'
	else
		$dynpc_notes[dynpc_cnt]     = 'Girlfriend<br><a href="exec:npcme_curloverrel = loverrelation[<<npcme_lover_num>>] & lover_number = <<npcme_lover_num>> & *clr & cla & gs ''lover_likes'', ''allPref'' & loverrelation[<<npcme_lover_num>>] = npcme_curloverrel & act ''Hang up.'': gt ''npcmenu'', ''start''">Call him and ask how he thinks you look?</a>'
		!$dynpc_notes[dynpc_cnt]     = 'Girlfriend<br><a href="exec:lover_number = <<npcme_lover_num>> & *clr & cla & gs ''lover_likes'', ''allPref'' & act ''Hang up.'': gt ''npcmenu'', ''start''">Call him and ask how he thinks you look?</a>'
	end
	dynpc_cnt += 1
	npcme_lover_num += 1
	if npcme_lover_num < npcme_lover_count: jump 'next_lover'
	:end
	
	!killvar 'npcme_tracked_dynpcs'
	!killvar 'dynpc_tracked'
	npcme_tracked_dynpcs_cnt = 0
	npcme13_dynpc_num = 0
	:next_npc
	if dynpc_tracked[npcme13_dynpc_num] = 1:
		npcme_tracked_dynpcs[npcme_tracked_dynpcs_cnt] = npcme13_dynpc_num
		npcme_tracked_dynpcs_cnt += 1
	end
	npcme13_dynpc_num += 1
	if npcme13_dynpc_num < dynpc_cnt: jump 'next_npc'
	
end

! [14]
if $ARGS[0] = 'add_npcs_sorted_by_groups':
	npcme14_groupnum = ARGS[1]
	npcme14_npc_num = 1
	:next_npc
	if npc_grupTipe['A<<npcme14_npc_num>>'] = npcme14_groupnum:
		npcme_npcs_sorted_by_groups[npcme_npcs_sorted_by_groups_cnt+1] = npcme14_npc_num
		npcme_npcs_sorted_by_groups_cnt += 1
	end
	npcme14_npc_num += 1
	if npcme14_npc_num <= npc_cnt: jump 'next_npc'
end

! [15]
if $ARGS[0] = 'save_preset':
	! ?killvar 'npcme_npc_tracked_preset<<ARGS[1]>>'
	! ?killvar 'npcme_dynpc_tracked_preset<<ARGS[1]>>'
	
	COPYARR 'npcme_npc_tracked_preset<<ARGS[1]>>', 'npc_tracked'
	COPYARR 'npcme_dynpc_tracked_preset<<ARGS[1]>>', 'dynpc_tracked'
end

! [16]
if $ARGS[0] = 'load_preset':
	! ?killvar 'npc_tracked'
	! ?killvar COPYARR 'dynpc_tracked'
	
	COPYARR 'npc_tracked', 'npcme_npc_tracked_preset<<ARGS[1]>>'
	COPYARR 'dynpc_tracked', 'npcme_dynpc_tracked_preset<<ARGS[1]>>'
end

